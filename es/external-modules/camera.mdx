---
title: "Camera"
description: "Dale visión a tu kode dot con el módulo Camera"
icon: "camera"
---

**Módulo:** Standard <Icon icon="spa" iconType="solid" />

# Características
Con este módulo puedes desde **aprender a capturar imágenes hasta grabar vídeo.** Integra una cámara con un sensor OV5640 con una resolución de 5MP.

<Frame>
  <img width="400" src="/images/external-modules/camera/camera.png" />
</Frame>

## Conexión con el kode dot

<Frame>
  <img src="/images/external-modules/camera/camera-kodedot.png" />
</Frame>

## Esquema de conexión
La cámara está conectada al ESP32-S3 de la siguiente manera:

| Cámara  | ESP32-S3 |
| ------- | -------- |
| nRESET  | GPIO44   |
| SCL     | GPIO47   |
| SDA     | GPIO48   |
| PCLK    | GPIO13   |
| HREF    | GPIO43   |
| VSYNC   | GPIO42   |
| D2      | GPIO12   |
| D3      | GPIO11   |
| D4      | GPIO1    |
| D5      | GPIO2    |
| D6      | GPIO3    |
| D7      | GPIO39   |
| D8      | GPIO40   |
| D9      | GPIO41   |
  
## Ejemplo de código
Para probar la cámara vamos a usar el ejemplo que incorpora **Espressif en Arduino IDE**. Puedes abrirlo en File > Examples > ESP32 > Camera > CameraWebServer.

Este ejemplo te permite configurar la cámara y ver en tiempo real lo que está capturando a través de una página web.

Cambia todo el código del CameraWebServer.ino por el siguiente y cambia las credenciales de la red WiFi por las tuyas:
```cpp CameraWebServer.ino lines icon="microchip"
#include "esp_camera.h"
#include <WiFi.h>

// ===========================
// Enter your WiFi credentials
// ===========================
const char *ssid = "**********";
const char *password = "**********";

void startCameraServer();
void setupLedFlash();

void setup() {
  Serial.begin(115200);
  Serial.setDebugOutput(true);
  Serial.println();

  camera_config_t config;
  config.ledc_channel = LEDC_CHANNEL_0;
  config.ledc_timer = LEDC_TIMER_0;
  config.pin_d0 = 12;
  config.pin_d1 = 11;
  config.pin_d2 = 1;
  config.pin_d3 = 2;
  config.pin_d4 = 3;
  config.pin_d5 = 39;
  config.pin_d6 = 40;
  config.pin_d7 = 41;
  config.pin_xclk = -1;
  config.pin_pclk = 13;
  config.pin_vsync = 42;
  config.pin_href = 43;
  config.pin_sccb_sda = 48;
  config.pin_sccb_scl = 47;
  config.pin_pwdn = -1;
  config.pin_reset = 44;
  config.xclk_freq_hz = 20000000;
  config.frame_size = FRAMESIZE_UXGA;
  config.pixel_format = PIXFORMAT_JPEG;  // for streaming
  //config.pixel_format = PIXFORMAT_RGB565; // for face detection/recognition
  config.grab_mode = CAMERA_GRAB_WHEN_EMPTY;
  config.fb_location = CAMERA_FB_IN_PSRAM;
  config.jpeg_quality = 12;
  config.fb_count = 1;

  // if PSRAM IC present, init with UXGA resolution and higher JPEG quality
  //                      for larger pre-allocated frame buffer.
  if (config.pixel_format == PIXFORMAT_JPEG) {
    if (psramFound()) {
      config.jpeg_quality = 10;
      config.fb_count = 2;
      config.grab_mode = CAMERA_GRAB_LATEST;
    } else {
      // Limit the frame size when PSRAM is not available
      config.frame_size = FRAMESIZE_SVGA;
      config.fb_location = CAMERA_FB_IN_DRAM;
    }
  } else {
    // Best option for face detection/recognition
    config.frame_size = FRAMESIZE_240X240;
#if CONFIG_IDF_TARGET_ESP32S3
    config.fb_count = 2;
#endif
  }

#if defined(CAMERA_MODEL_ESP_EYE)
  pinMode(13, INPUT_PULLUP);
  pinMode(14, INPUT_PULLUP);
#endif

  // camera init
  esp_err_t err = esp_camera_init(&config);
  if (err != ESP_OK) {
    Serial.printf("Camera init failed with error 0x%x", err);
    return;
  }

  sensor_t *s = esp_camera_sensor_get();
  // initial sensors are flipped vertically and colors are a bit saturated

  // drop down frame size for higher initial frame rate
  if (config.pixel_format == PIXFORMAT_JPEG) {
    s->set_framesize(s, FRAMESIZE_QVGA);
  }
  
  s->set_vflip(s, 1);

  WiFi.begin(ssid, password);
  WiFi.setSleep(false);

  Serial.print("WiFi connecting");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.println("WiFi connected");

  startCameraServer();

  Serial.print("Camera Ready! Use 'http://");
  Serial.print(WiFi.localIP());
  Serial.println("' to connect");
}

void loop() {
  // Do nothing. Everything is done in another task by the web server
  delay(10000);
}
```



