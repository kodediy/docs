---
title: "Micrófono"
# description: "Micrófono"
icon: "microphone"
---

# Características
El kode dot integra un **micrófono MEMS digital.** Este tipo de micrófonos son muy comunes en dispositivos electrónicos modernos y dan una gran fiabilidad ya que **no dependen de ninguna parte analógica.**

## Esquema de conexión
El micrófono está conectado al ESP32-S3 de la siguiente manera: 

| Micrófono    | ESP32-S3 |
| ------------ | -------- |
| SCK          | GPIO38   |
| WS           | GPIO45   |
| DIN          | GPIO21   |

## Librerías recomendadas
### Arduino
- [ESP_I2S](https://github.com/espressif/arduino-esp32/blob/master/libraries/ESP_I2S)

### ESP-IDF
- [ESP-ADF](https://github.com/espressif/esp-adf)

<Warning>El uso de ESP-ADF es para usuarios avanzados. Si no tienes experiencia en este framework, te recomendamos usar la librería de Arduino.</Warning>

## Ejemplo de código
Este código **graba 5 segundos de audio** y lo guarda en la microSD en formato WAV.

```cpp record_to_microsd.ino lines icon="microchip"
#include "ESP_I2S.h"
#include "FS.h"
#include "SD_MMC.h"

/* Asignación de pines para I2S */
const uint8_t I2S_SCK   = 38;  /* Pin de reloj serial (SCK) */
const uint8_t I2S_WS    = 45;  /* Pin de selección de palabra (LRCLK) */
const uint8_t I2S_DIN   = 21;  /* Pin de datos de entrada (SD) */

/* Asignación de pines para tarjeta SD en modo 1-bit con SD_MMC */
const uint8_t SD_CMD    = 5;   /* Pin de comando (CMD) */
const uint8_t SD_CLK    = 6;   /* Pin de reloj (CLK) */
const uint8_t SD_DATA0  = 7;   /* Pin de datos 0 (D0) */

void setup() {
  /* Crear instancia de la clase I2S */
  I2SClass i2s;

  /* Variables para almacenar los datos WAV y su tamaño */
  uint8_t *wav_buffer;
  size_t wav_size;

  /* Inicializar puerto serie para depuración */
  Serial.begin(115200);
  Serial.println("Iniciando configuración...");

  /* Configurar los pines I2S (no se usa MCLK: pasar -1) */
  i2s.setPins(I2S_SCK, I2S_WS, -1, I2S_DIN);

  /* Inicializar I2S en modo estándar: 16 kHz, 32 bits de datos, mono, alineación a la izquierda */
  Serial.println("Inicializando bus I2S...");
  if (!i2s.begin(
        I2S_MODE_STD,
        16000,
        I2S_DATA_BIT_WIDTH_32BIT,
        I2S_SLOT_MODE_MONO,
        I2S_STD_SLOT_LEFT)) {
    Serial.println("¡Error al inicializar el bus I2S!");
    return;  /* Abortar si falla inicialización I2S */
  }
  Serial.println("Bus I2S inicializado.");

  /* Configurar pines de SD_MMC para modo 1-bit */
  Serial.println("Configurando pines de la tarjeta SD...");
  if (!SD_MMC.setPins(SD_CLK, SD_CMD, SD_DATA0)) {
    Serial.println("¡Error al configurar pines SD!");
    return;
  }

  /* Montar la tarjeta SD en "/sdcard" */
  Serial.println("Montando tarjeta SD...");
  if (!SD_MMC.begin("/sdcard", true)) {
    Serial.println("¡Error al inicializar la tarjeta SD!");
    return;
  }
  Serial.println("Tarjeta SD montada correctamente.");

  /* Informar que comenzará la grabación */
  Serial.println("Grabando 5 segundos de audio...");

  /* Grabar datos WAV durante 5 segundos en wav_buffer */
  wav_buffer = i2s.recordWAV(5, &wav_size);

  /* Abrir un archivo en la tarjeta SD para escritura */
  File file = SD_MMC.open("/test.wav", FILE_WRITE);
  if (!file) {
    Serial.println("¡Error al abrir el archivo para escribir!");
    return;
  }

  /* Escribir datos grabados y verificar escritura completa */
  Serial.println("Escribiendo datos de audio en el archivo...");
  if (file.write(wav_buffer, wav_size) != wav_size) {
    Serial.println("¡Error al escribir datos de audio en el archivo!");
    file.close();
    return;
  }

  /* Cerrar el archivo al finalizar */
  file.close();
  Serial.println("Grabación de audio y guardado completados.");
}

void loop() {
  /* Bucle vacío: todo el trabajo se realiza en setup */
}
```

## Descarga de ejemplos
Puedes probar los códigos de ejemplo mediante el IDE de Arduino o el IDE de ESP-IDF o descargar los códigos en nuestro drive:

[Ejemplos de audio](https://drive.google.com/drive/folders/1rJPxfOXun4p1ijXyRdREydyOMEUJju0u)