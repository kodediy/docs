---
title: "Altavoz"
# description: "Altavoz"
icon: "volume-high"
---

# Características

El kode dot integra un altavoz de 1W conectado a un amplificador. Así, solo te tienes que preocupar de darle sonido a tus proyectos a través del bus I2S.

## Esquema de conexión

El micrófono está conectado al ESP32-S3 de la siguiente manera:

| Micrófono | ESP32-S3 |
| --------- | -------- |
| SCK       | GPIO38   |
| WS        | GPIO45   |
| DOUT      | GPIO46   |
| SD        | P3       |

<Note>
  El amplificador por defecto está apagado para no gastar energía. Para encenderlo, pon el pin EXP3 del [expansor de pines](/es/kode-dot/io-expander) en HIGH.
</Note>

## Librerías recomendadas

### Arduino

- [ESP_I2S](https://github.com/espressif/arduino-esp32/blob/master/libraries/ESP_I2S)
- [ESP32_IO_Expander](https://github.com/esp-arduino-libs/ESP32_IO_Expander)

### ESP-IDF

- [ESP-ADF](https://github.com/espressif/esp-adf)
- [esp_io_expander_tca95xx_16bit](https://components.espressif.com/components/espressif/esp_io_expander_tca95xx_16bit)

<Warning>
  El uso de ESP-ADF es para usuarios avanzados. Si no tienes experiencia en este
  framework, te recomendamos usar la librería de Arduino.
</Warning>

## Códigos de ejemplo
Este código reproduce el tono que especifiques en la variable `frequency`.
```cpp speaker_tone.ino lines icon="microchip"
#include <Arduino.h>
#include <ESP_I2S.h>
#include <esp_io_expander.hpp>

/* Configuración del expansor de pines */
#define CHIP_NAME       TCA95XX_16BIT
#define I2C_SCL_PIN     (47)
#define I2C_SDA_PIN     (48)
#define EXP_INT_PIN     (18)
#define I2C_ADDR        (0x20)

/* Pines para la interfaz I2S */
const uint8_t I2S_SCK   = 38;  // Reloj serial (SCK)
const uint8_t I2S_WS    = 45;  // Word Select / LRCLK
const uint8_t I2S_DOUT  = 46;  // Datos de salida (SD)

/* Parámetros de la señal de audio */
const int frequency   = 440;    // Frecuencia de la onda cuadrada en Hz
const int amplitude   = 500;    // Amplitud de la onda cuadrada
const int sampleRate  = 16000;  // Frecuencia de muestreo en Hz

/* Formato de datos y modo I2S */
i2s_data_bit_width_t bps = I2S_DATA_BIT_WIDTH_32BIT;  // Bits por muestra
i2s_mode_t mode           = I2S_MODE_STD;            // Modo estándar
i2s_slot_mode_t slot      = I2S_SLOT_MODE_MONO;       // Mono

/* Cálculo de medio periodo de la onda */
const int halfWavelength = (sampleRate / frequency);

/* Variables de estado de la onda */
int32_t sample = amplitude;  // Valor de la muestra actual
int count = 0;               // Contador de muestras

/* Instancias globales */
I2SClass i2s;                // Objeto I2S
esp_expander::Base *expander = nullptr;  // Puntero al expansor

void setup() {
  Serial.begin(115200);
  Serial.println("I2S tono simple");

  /* Configurar los pines I2S (MCLK no usado: -1) */
  i2s.setPins(I2S_SCK, I2S_WS, I2S_DOUT, -1);

  /* Iniciar I2S con los parámetros definidos */
  if (!i2s.begin(mode, sampleRate, bps, slot, I2S_STD_SLOT_LEFT)) {
    Serial.println("Error al inicializar I2S!");
    while (1);  // Detener si falla
  }
  Serial.println("Bus I2S inicializado.");

  /* Inicializa el expansor */
  expander = new esp_expander::TCA95XX_16BIT(I2C_SCL_PIN, I2C_SDA_PIN, I2C_ADDR);
  expander->init();
  expander->begin();

  /* Configurar el pin 3 del expansor como salida para habilitar el amplificador */
  expander->pinMode(3, OUTPUT);
  expander->digitalWrite(3, HIGH); /* Activar amplificador */
}

void loop() {
  /* Cada medio periodo, invertir la fase de la onda */
  if (count % halfWavelength == 0) {
    sample = -sample;
  }

  /* Escribir la muestra en ambos canales (mono duplicado) */
  i2s.write(sample); // Canal derecho
  i2s.write(sample); // Canal izquierdo

  /* Incrementar contador de muestras */
  count++;
}
```

## Descarga del código
Puedes probar los códigos de ejemplo mediante el IDE de Arduino o el IDE de ESP-IDF o descargar los códigos en nuestro drive:

[Ejemplos de audio](https://drive.google.com/drive/folders/1rJPxfOXun4p1ijXyRdREydyOMEUJju0u)